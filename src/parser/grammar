expr = term ( '+' term | '-' term )*

term = factor next ( '*' factor next | '/' factor next )*

factor = next unit factor_rest
factor_rest = '.' IDENTIFIER next method_or_field factor_rest
            | ε
method_or_field = '(' args
                | ε

unit = 'if' expr block next [ 'else' next block ]
     | 'while' expr block
     | 'loop' next block
     | 'for' next IDENTIFIER next 'in' expr block 
     | 'type' next IDENTIFIER next '{' named_args
     | 'incl' next IDENTIFIER next [ 'as' next IDENTIFIER ]
     | 'mut' next IDENTIFIER next '=' expr (* mutable variable assigment *)
     | '@' next IDENTIFIER next '(' args
     | 'func' function_declaration block
     | 'test' function_declaration block
     | 'mock' function_declaration block
     | 'extern' 'func' function_declaration ';'
     | 'return' expr
     | '{' next inner_block
     | 'true'
     | 'false'
     | "'" CHAR "'"
     | '"' [^"] '"'
     | INT
     | DOUBLE
     | IDENTIFIER next func_type_or_var

method_or_field = '(' args                    (* method call *)
                | ε                           (* field access *)

func_type_or_var = '(' next func_or_type_args
                 | '=' expr                   (* variable assigment *)
                 | ε                          (* variable *)

(* Technically not LL(1) but should allow for better performance *)
func_or_type_args = IDENTIFIER next ':' expr (',' typed_arg )* ')' (* type_instanciation *)
                  | args                                           (* function_call *)


block = '{' next inner_block
inner_block = expr ( ';' expr )* '}'
            | '}'

function_declaration = next IDENTIFIER next '(' typed_args next return_type
return_type = '->' next IDENTIFIER next
            | ε

typed_args = typed_arg ( ',' typed_arg )* ')'
           | ')'
typed_arg = next IDENTIFIER next ':' expr

named_args = named_arg ( ',' named_arg )* '}'
           | '}'
named_arg = next IDENTIFIER next '=' next expr next

args = expr ( ',' expr )* ')'
     | ')'

next = extra*

extra = WHITESPACE
      | '/*' [^'*/'] '*/'
      | '//' [^\n] '\n'
      | '#'  [^\n] '\n'
